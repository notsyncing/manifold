buildscript {
    ext.kotlin_version = '1.1-M01'
    //ext.kotlin_version = '1.1.0-dev-2972'

    repositories {
        mavenCentral()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
            //url "http://dl.bintray.com/kotlin/kotlin-dev"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

def ver = '0.1.3'
def grp = 'io.github.notsyncing.manifold'

group grp
version ver

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

javadoc {
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()

    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        //url "http://dl.bintray.com/kotlin/kotlin-dev"
    }

    maven {
        url "http://dl.bintray.com/kotlin/kotlinx.coroutines"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-async:0.1-alpha'
    compile 'io.vertx:vertx-core:3.3.2'
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.16'
    compile group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '2.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'io.vertx:vertx-unit:3.3.2'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId grp
            artifactId 'manifold'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'manifold'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/manifold.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}